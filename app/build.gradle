apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

String readCommitHash() {
    def suffix = ''

    def gitExists = exec {
        commandLine 'which', 'git'
        ignoreExitValue true
    }

    if (gitExists.getExitValue() == 0) {
        def so = new ByteArrayOutputStream()
        def gitRevParseResult = exec {
            executable 'git'
            args 'rev-parse', '--short', 'HEAD'
            ignoreExitValue true
            standardOutput = so
        }

        if (gitRevParseResult.exitValue == 0) {
            suffix = '.' + so.toString().trim()
        }
    }

    return suffix
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "sword.notes.android"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0" + readCommitHash()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
